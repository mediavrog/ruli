buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'java'
sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    lib_name = 'ruli'
    lib_group = 'net.mediavrog'
    lib_version = '1.1.0'
    lib_version_comment = 'Immutable classes, docs and tests.'
    lib_tag = '1.1.0'
}

group = lib_group
version = lib_version

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

// PUBLISHING
publishing {
    publications {
        DefaultPublication(MavenPublication) {
            from components.java
            groupId lib_group
            artifactId lib_name
            version lib_version

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    pkg {
        repo = 'maven'
        name = lib_name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mediavrog/ruli.git'
        labels = ['rule', 'rule-engine', 'java']
        publications = ['DefaultPublication']
        version {
            name = lib_version
            desc = lib_version_comment
            vcsTag = lib_tag
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}